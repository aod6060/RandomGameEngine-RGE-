/*
  skybox_frag.glsl

  This is the skybox renderer fragment shader

  // Sources
  // Used for theory
  [1] http://http.developer.nvidia.com/GPUGems2/gpugems2_chapter16.html
  // Used as a reference for glsl
  [2] https://github.com/fcvarela/tws/blob/master/data/shaders/atmosphere.glsl
  // Used as a reference for glsl
  [3] https://github.com/wwwtyro/glsl-atmosphere
*/
#version 330 core

// Structures
struct Light {
  vec3 diffuse;
  vec3 specular;
  vec3 position;
};

// Uniform
uniform Light light;
// Ray Origin
uniform vec3 rayOrigin;
// Sun Intencity
uniform float sunIntencity;
// Inner Radius
uniform float innerRadius;
// Outer Radius
uniform float outerRadius;
// Rays Scattering Coefficient
uniform vec3 reisCoefficient;
// Mie Scattering Coefficient
uniform float miesCofficient;
// Rays Scale height
uniform float reisScaleHeight;
// Mie Scale height
uniform float miesScaleHeight;
// Mie Preferred Scattering Direction
uniform float miesPreScatDir;
// Input
in vec3 v_pos;
// Output
out vec4 out_color;

#define PI 3.141592
#define iSteps 16
#define jSteps 8

float rsi(vec3 r0, vec3 rd, float sr) {
    // Simplified ray-sphere intersection that assumes
    // the ray starts inside the sphere and that the
    // sphere is centered at the origin. Always intersects.
    float a = dot(rd, rd);
    float b = 2.0 * dot(rd, r0);
    float c = dot(r0, r0) - (sr * sr);
    return (-b + sqrt((b*b) - 4.0*a*c))/(2.0*a);
}

vec3 atmosphere(vec3 r, vec3 r0, vec3 pSun, float iSun, float rPlanet, float rAtmos, vec3 kRlh, float kMie, float shRlh, float shMie, float g) {
    // Normalize the sun and view directions.
    pSun = normalize(pSun);
    r = normalize(r);

    // Calculate the step size of the primary ray.
    float iStepSize = rsi(r0, r, rAtmos) / float(iSteps);

    // Initialize the primary ray time.
    float iTime = 0.0;

    // Initialize accumulators for Rayleigh and Mie scattering.
    vec3 totalRlh = vec3(0,0,0);
    vec3 totalMie = vec3(0,0,0);

    // Initialize optical depth accumulators for the primary ray.
    float iOdRlh = 0.0;
    float iOdMie = 0.0;

    // Calculate the Rayleigh and Mie phases.
    float mu = dot(r, pSun);
    float mumu = mu * mu;
    float gg = g * g;
    float pRlh = 3.0 / (16.0 * PI) * (1.0 + mumu);
    float pMie = 3.0 / (8.0 * PI) * ((1.0 - gg) * (mumu + 1.0)) / (pow(1.0 + gg - 2.0 * mu * g, 1.5) * (2.0 + gg));

    // Sample the primary ray.
    for (int i = 0; i < iSteps; i++) {

        // Calculate the primary ray sample position.
        vec3 iPos = r0 + r * (iTime + iStepSize * 0.5);

        // Calculate the height of the sample.
        float iHeight = length(iPos) - rPlanet;

        // Calculate the optical depth of the Rayleigh and Mie scattering for this step.
        float odStepRlh = exp(-iHeight / shRlh) * iStepSize;
        float odStepMie = exp(-iHeight / shMie) * iStepSize;

        // Accumulate optical depth.
        iOdRlh += odStepRlh;
        iOdMie += odStepMie;

        // Calculate the step size of the secondary ray.
        float jStepSize = rsi(iPos, pSun, rAtmos) / float(jSteps);

        // Initialize the secondary ray time.
        float jTime = 0.0;

        // Initialize optical depth accumulators for the secondary ray.
        float jOdRlh = 0.0;
        float jOdMie = 0.0;

        // Sample the secondary ray.
        for (int j = 0; j < jSteps; j++) {

            // Calculate the secondary ray sample position.
            vec3 jPos = iPos + pSun * (jTime + jStepSize * 0.5);

            // Calculate the height of the sample.
            float jHeight = length(jPos) - rPlanet;

            // Accumulate the optical depth.
            jOdRlh += exp(-jHeight / shRlh) * jStepSize;
            jOdMie += exp(-jHeight / shMie) * jStepSize;

            // Increment the secondary ray time.
            jTime += jStepSize;
        }

        // Calculate attenuation.
        vec3 attn = exp(-(kMie * (iOdMie + jOdMie) + kRlh * (iOdRlh + jOdRlh)));

        // Accumulate scattering.
        totalRlh += odStepRlh * attn;
        totalMie += odStepMie * attn;

        // Increment the primary ray time.
        iTime += iStepSize;

    }

    // Calculate and return the final color.
    return iSun * (pRlh * kRlh * totalRlh + pMie * kMie * totalMie);
}

void main() {
  vec3 color = atmosphere(
    normalize(v_pos),
    rayOrigin,
    -light.position,
    sunIntencity,
    innerRadius,
    outerRadius,
    reisCoefficient,
    miesCofficient,
    reisScaleHeight,
    miesScaleHeight,
    miesPreScatDir
    );

    color = 1.0 - exp(-2.0 * color);

    out_color = vec4(color, 1.0);
}
